# Maintainer: Sven Schneider <archlinux.sandmann@googlemail.com>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>

pkgname=eclipse-mylyn
pkgver=3.8.2
_reldate=20120916-1200
pkgrel=1
pkgdesc="A task-focused interface for Eclipse"
arch=('any')
url="http://www.eclipse.org/mylyn/"
license=('EPL')
depends=('eclipse')
makedepends=('unzip' 'java-environment')
optdepends=('bugzilla: ticketing support')
source=(http://download.eclipse.org/mylyn/drops/${pkgver}/v${_reldate}/mylyn-${pkgver}.v${_reldate}.zip)
replaces=('eclipse-mylyn-extras')
changelog=ChangeLog
provides=("eclipse-mylyn-extras=${pkgver}")
md5sums=('ba3408e8d95a3c8d32564532600f4f00')
sha256sums=('1279308d1e6710aea405ceefd33ff4c63bcbb57edcd01a2a60ca0736086ac682')
sha384sums=('c95b89241e5651cb9afa9568ee404b6bcbe59c62b4b159ccdae19fcd59f650c1e84f086a55457eafe65dc9d2a22b3b11')
sha512sums=('4b2ab561f36fe18423656bceb7e20928b38ffe5946f39952fc98c2cf4b634ccc2db36cf4cf3abce176c36f5220a9ef884762948a0c20951f118ebb146af79738')

build() {
  return 0
}

package() {
  _dest=${pkgdir}/usr/share/eclipse/dropins/${pkgname/eclipse-}/eclipse

  cd ${srcdir}

  # Features
  find features -type f | while read _feature ; do
    if [[ ${_feature} =~ (.*\.jar$) ]] ; then
      install -dm755 ${_dest}/${_feature%*.jar}
      cd ${_dest}/${_feature/.jar}
      jar xf ${srcdir}/${_feature} || return 1
      cd - >/dev/null 2>&1
    elif [[ ! ${_feature} =~ (.*\.jar.pack.gz) ]] || [ ! -e ${_feature%\.pack\.gz$} ] ; then
      install -Dm644 ${_feature} ${_dest}/${_feature}
    fi
  done

  # Plugins
  find plugins -type f | while read _plugin ; do
    if [[ ! ${_plugin} =~ (.*\.jar.pack.gz) ]] || [ ! -e ${_plugin%\.pack\.gz$} ] ; then
      install -Dm644 ${_plugin} ${_dest}/${_plugin}
    fi
  done
}
