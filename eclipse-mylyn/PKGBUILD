# Maintainer: Sven Schneider <archlinux.sandmann@googlemail.com>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>

pkgname=eclipse-mylyn
pkgver=3.10.0
_reldate=20131025-2037
pkgrel=1
pkgdesc="A task-focused interface for Eclipse"
arch=('any')
url="http://www.eclipse.org/mylyn/"
license=('EPL')
depends=('eclipse')
makedepends=('unzip' 'java-environment')
optdepends=('bugzilla: ticketing support')
source=(http://download.eclipse.org/mylyn/drops/${pkgver}/v${_reldate}/mylyn-${pkgver}.v${_reldate}.zip)
replaces=('eclipse-mylyn-extras')
changelog=ChangeLog
provides=("eclipse-mylyn-extras=${pkgver}")
md5sums=('d333922a9e2acb7166bcf4781b82ad7e')
sha256sums=('083f9379e9fb1e779f3c0bab08209c1a9c00657d984cbc0a956501142f9b68a4')
sha384sums=('ab46ca13ceedb81c99ca5b56721503fefc389796bef3ff134d6d8843fe519d8290d5d2bb8f0bdcad45e1382c724b7fc6')
sha512sums=('9dd7faaf642be4a5425caf1ccbcdcec8233d70fced9477f5690754629925669a4e6f442462ee3501117faf966bf2f9de95f57bbd28814ac947243c5b4b1f689b')

package() {
  _dest=${pkgdir}/usr/share/eclipse/dropins/${pkgname/eclipse-}/eclipse

  cd ${srcdir}

  # Features
  find features -type f | while read _feature ; do
    if [[ ${_feature} =~ (.*\.jar$) ]] ; then
      install -dm755 ${_dest}/${_feature%*.jar}
      cd ${_dest}/${_feature/.jar}
      jar xf ${srcdir}/${_feature} || return 1
      cd - >/dev/null 2>&1
    elif [[ ! ${_feature} =~ (.*\.jar.pack.gz) ]] || [ ! -e ${_feature%\.pack\.gz$} ] ; then
      install -Dm644 ${_feature} ${_dest}/${_feature}
    fi
  done

  # Plugins
  find plugins -type f | while read _plugin ; do
    if [[ ! ${_plugin} =~ (.*\.jar.pack.gz) ]] || [ ! -e ${_plugin%\.pack\.gz$} ] ; then
      install -Dm644 ${_plugin} ${_dest}/${_plugin}
    fi
  done
}
