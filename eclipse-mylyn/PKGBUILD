# Maintainer: Sven Schneider <archlinux.sandmann@googlemail.com>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>

pkgname=eclipse-mylyn
pkgver=3.15.0
_reldate=20150331-1812
pkgrel=1
pkgdesc="A task-focused interface for Eclipse"
arch=('any')
url="http://www.eclipse.org/mylyn/"
license=('EPL')
depends=('eclipse')
makedepends=('unzip' 'java-environment')
optdepends=('bugzilla: ticketing support')
source=(http://download.eclipse.org/mylyn/drops/${pkgver}/v${_reldate}/mylyn-${pkgver}.v${_reldate}.zip)
replaces=('eclipse-mylyn-extras')
changelog=ChangeLog
provides=("eclipse-mylyn-extras=${pkgver}")
md5sums=('d7236ca648c0a6d0e7fa370f88c7a855')
sha256sums=('438220e8947b5680496d1fa166025da8add03141a82d2d99ec9025a12b837c30')
sha384sums=('d12138d58b1c4ef0d8fe035058fcaa091d9a4abadc3e19e0c691c2f68f0f1b62d8c1a7468fd55ece73a754a71e50f1ec')
sha512sums=('7a0265340f8ca9daaf1e31d8eef155cde9f4424153661f84f39d28cee1af08ef479eeb7b7b1af928299769f17522e0129c818ce3594d07d959dc008b0270c408')

package() {
  _dest=${pkgdir}/usr/share/eclipse/dropins/${pkgname/eclipse-}/eclipse

  cd ${srcdir}

  # Features
  find features -type f | while read _feature ; do
    if [[ ${_feature} =~ (.*\.jar$) ]] ; then
      install -dm755 ${_dest}/${_feature%*.jar}
      cd ${_dest}/${_feature/.jar}
      jar xf ${srcdir}/${_feature} || return 1
      cd - >/dev/null 2>&1
    elif [[ ! ${_feature} =~ (.*\.jar.pack.gz) ]] || [ ! -e ${_feature%\.pack\.gz$} ] ; then
      install -Dm644 ${_feature} ${_dest}/${_feature}
    fi
  done

  # Plugins
  find plugins -type f | while read _plugin ; do
    if [[ ! ${_plugin} =~ (.*\.jar.pack.gz) ]] || [ ! -e ${_plugin%\.pack\.gz$} ] ; then
      install -Dm644 ${_plugin} ${_dest}/${_plugin}
    fi
  done
}
