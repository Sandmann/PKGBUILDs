# Maintainer: Sven Schneider <archlinux.sandmann@googlemail.com>
# Contributor: Jason Melton <jason.melton@gmail.com>

pkgname=stuntrally
pkgver=1.6
pkgrel=1
pkgdesc="Stunt Rally game with track editor, based on VDrift and OGRE"
arch=('i686' 'x86_64')
license=('GPL3')
url="http://code.google.com/p/vdrift-ogre"
depends=('libvorbis' 'mygui' 'sdl' 'enet' 'bullet' 'boost')
makedepends=('cmake')
source=(src_${pkgver}.tgz::https://nodeload.github.com/stuntrally/stuntrally/tarball/${pkgver}
	tracks_${pkgver}.tgz::https://nodeload.github.com/stuntrally/tracks/tarball/${pkgver})
md5sums=('e4a6e2415f792f097913b2ef70ad7f3a'
		 'd9661fab7d5ca9067d47c81ce702fd10')

build() {
	# build the sources
	cd "${srcdir}/stuntrally-stuntrally-c769606/"
	
	rm -rf build
	mkdir build && cd build
	
	cmake -DCMAKE_INSTALL_PREFIX="/usr" -DSHARE_INSTALL="share/stuntrally/" ..
	make
	
	
	# build the tracks
	cd "${srcdir}/stuntrally-tracks-a96e554/"
	
	cmake -DCMAKE_INSTALL_PREFIX="/usr" .
	make
}

package() {
	#install the sources
	cd "${srcdir}/stuntrally-stuntrally-c769606/build/"
	
	make DESTDIR="${pkgdir}" install
	
	
	# install the tracks
	cd "${srcdir}/stuntrally-tracks-a96e554/"

	make DESTDIR="${pkgdir}/usr/share/stuntrally/" install
}
