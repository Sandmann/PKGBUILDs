# Maintainer: Sven Schneider <archlinux.sandmann@googlemail.com>
# Contributor: Jason Melton <jason.melton@gmail.com>

pkgname=stuntrally
pkgver=1.7
pkgrel=1
pkgdesc="Stunt Rally game with track editor, based on VDrift and OGRE"
arch=('i686' 'x86_64')
license=('GPL3')
url="http://code.google.com/p/vdrift-ogre"
depends=('libvorbis' 'mygui' 'sdl' 'enet' 'bullet' 'boost')
makedepends=('cmake')
source=(src_${pkgver}.tgz::https://nodeload.github.com/stuntrally/stuntrally/tarball/${pkgver}
	tracks_${pkgver}.tgz::https://nodeload.github.com/stuntrally/tracks/tarball/${pkgver})
md5sums=('d63ae9b35b6b263c1bedc16fcb957246'
		 'b3e4c3ce52256c801e86f763c9f80b36')

build() {
	# build the sources
	cd "${srcdir}/stuntrally-stuntrally-37e6836/"
	
	rm -rf build
	mkdir build && cd build
	
	cmake -DCMAKE_INSTALL_PREFIX="/usr" -DSHARE_INSTALL="share/stuntrally/" ..
	make
	
	
	# build the tracks
	cd "${srcdir}/stuntrally-tracks-76fb261/"
	
	cmake -DCMAKE_INSTALL_PREFIX="/usr" .
	make
}

package() {
	#install the sources
	cd "${srcdir}/stuntrally-stuntrally-37e6836/build/"
	
	make DESTDIR="${pkgdir}" install
	
	
	# install the tracks
	cd "${srcdir}/stuntrally-tracks-76fb261/"

	make DESTDIR="${pkgdir}/usr/share/stuntrally/" install
}
