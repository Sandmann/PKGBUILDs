# Maintainer: Sven Schneider <archlinux.sandmann@googlemail.com>
# Contributor: Jason Melton <jason.melton@gmail.com>

pkgname=stuntrally
pkgver=1.8
pkgrel=1
pkgdesc="Stunt Rally game with track editor, based on VDrift and OGRE"
arch=('i686' 'x86_64')
license=('GPL3')
url="http://code.google.com/p/vdrift-ogre"
depends=('libvorbis' 'mygui' 'sdl' 'enet' 'bullet')
makedepends=('cmake' 'boost')
source=(src_${pkgver}.tar.gz::https://nodeload.github.com/${pkgname}/stuntrally/tar.gz/${pkgver}
        tracks_${pkgver}.tar.gz::https://nodeload.github.com/${pkgname}/tracks/tar.gz/${pkgver}
        shiny.patch)
md5sums=('88fc1e06ad2b0b23fd0e3af338fcf5a9'
         '3dc7b2bd2eeb3efbb8430040db56c7d0'
         'dc4d04ba6945a9d0a6871055a47c53fc')
sha256sums=('485c328aa9a8ff26f41aeb4c1ecddf019ff83287ea88f50c3f4df4c9839d65a4'
            'a10e52e81c4717d755d6e7bf3997c1bebc3984749399a5943e8b70b4d2381017'
            '8161c6e1778eb829fb36139b3095902f1cefc348367e316c226cbbf7e5106bee')
sha384sums=('2e0b11df51120b554ea1efde96f3e1c7f17ab7eebbcc7c10e4bf2578fbb2d0df39cb07937662797e07abf9a753fbe708'
            '814509508b62dbea9bd187ce03ea475f1f107cd182b9b2a13b554829e03a45b642086de558badf11ee72a22708f0fac5'
            '6d6a8747c7adfee3ec0aa7060bcd6db1589214c9a295af4c953242ca4c81db11f5306ccacfaf7c73ca6624fe72667988')
sha512sums=('faa8b2107ad8f24aaff21dcf7b5d9d1834f0511a4d11fb1ca4c936dc060b040f50d3e028df0cf6308d146cf3ccd4e3749f5e495a4f9c0b676be8e1bc475a4b67'
            'b09b47372062c7dc9621eb62bb18896a913f6e2edde36633b7406e2ca633ad3c15345dc81fe72f27ca08ac4cc0f8ac0f013e75da7bbb2aff8366ac983ef6d1cd'
            'cc252eb2c6dfaad67ba67939b73e5dc83ed1abf51169198a17165aa7b4eefff7c2696064a757486ac062f6cde8a4d1d8754e5a56c488476e30db287234e15e3e')

build() {
  # build the sources
  cd "${srcdir}/stuntrally-${pkgver}/"
	
  patch -p1 < ${srcdir}/shiny.patch
  sed 's/materials objects/materials material_templates objects/g' -i data/CMakeLists.txt

  rm -rf build
  mkdir build && cd build

  cmake -DCMAKE_INSTALL_PREFIX="/usr" -DSHARE_INSTALL="share/stuntrally" ..
  make


  # build the tracks
  cd "${srcdir}/tracks-${pkgver}/"

  rm -rf build
  mkdir build && cd build

  cmake -DCMAKE_INSTALL_PREFIX="/usr" ..
  make
}

package() {
  #install the sources
  cd "${srcdir}/stuntrally-${pkgver}/build/"

  make DESTDIR="${pkgdir}" install


  # install the tracks
  cd "${srcdir}/tracks-${pkgver}/build"

  make DESTDIR="${pkgdir}/usr/share/stuntrally/" install

  rm -rf "${pkgdir}/usr/share/stuntrally/tracks/build"
}
