# Maintainer: Sven Schneider <archlinux.sandmann@googlemail.com>
# Contributor: Jason Melton <jason.melton@gmail.com>

pkgname=stuntrally
pkgver=1.4
pkgrel=2
pkgdesc="Stunt Rally game with track editor, based on VDrift and OGRE"
arch=('i686' 'x86_64')
license=('GPL3')
url="http://code.google.com/p/vdrift-ogre"
depends=('libvorbis' 'mygui' 'sdl')
makedepends=('cmake' 'boost')
source=(src.tgz::https://nodeload.github.com/stuntrally/stuntrally/tarball/1.4
	tracks.tgz::https://nodeload.github.com/stuntrally/tracks/tarball/1.4)
md5sums=('5b91e8429ae2101584b64e09c6e87b26'
		 '44507258f4cec4c5b31a1446722e095e')

build() {
	# build the sources
	cd "${srcdir}/stuntrally-stuntrally-4692afe/"
	
	rm -rf build
	mkdir build && cd build
	
	cmake -DCMAKE_INSTALL_PREFIX="/usr" -DSHARE_INSTALL="share/stuntrally/" ..
	make
	
	
	# build the tracks
	cd "${srcdir}/stuntrally-tracks-b5a8a2d/"
	
	cmake -DCMAKE_INSTALL_PREFIX="/usr" .
	make
}

package() {
	#install the sources
	cd "${srcdir}/stuntrally-stuntrally-4692afe/build/"
	
	make DESTDIR="${pkgdir}" install
	
	
	# install the tracks
	cd "${srcdir}/stuntrally-tracks-b5a8a2d/"

	make DESTDIR="${pkgdir}/usr/share/stuntrally/" install
}
