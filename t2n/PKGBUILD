# Maintainer: Sven Schneider <archlinux.sandmann@googlemail.com>

pkgname=t2n
pkgver=0.2
pkgrel=1
pkgdesc="Tool for communication between PC and Lego NXT"
arch=('i686' 'x86_64')
url="http://www-verimag.imag.fr/~raymond/edu/lego/t2n/"
license=('LGPL')
depends=('libusb')
source=(http://www-verimag.imag.fr/~raymond/edu/lego/t2n/${pkgname}-${pkgver}.src.tgz
        x86_64.diff)

md5sums=('37735172c794acd01ae20241c6c28d22'
         'b4f24861c713ffd885732339f85bf983')

build() {
	cd ${srcdir}/${pkgname}-${pkgver}.src

	if [ "${CARCH}" = "x86_64" ]; then
		# Fix for x86-64 architecture
		patch -p1 <${srcdir}/x86_64.diff || return 1
		BUILDARCH=x86_64
		_archdir=x86_64
	fi
	if [ "${CARCH}" = "i686" ]; then
		BUILDARCH=i386
		_archdir=x86
	fi

	make
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}.src

	# install the binaries
	install -D -m755 obj/t2n ${pkgdir}/usr/bin/t2n
	install -D -m755 obj/usbscan ${pkgdir}/usr/bin/usbscan
	
	# install udev rule
	install -D -m644 udev/70-lego.rules ${pkgdir}/etc/udev/rules.d/70-lego.rules
}

